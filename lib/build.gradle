apply plugin: 'com.android.application'
apply plugin: 'android-maven'
apply plugin: 'signing'

android {
    compileSdkVersion 21
    buildToolsVersion "21.0.2"

    defaultConfig {
        applicationId "com.willowtreeapps.trafficcop"
        minSdkVersion 15
        targetSdkVersion 21
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            runProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

apply plugin: 'android-unit-test'

dependencies {
    compile files('libs/com.bigandroidbbq-1-dex2jar.jar')

    testCompile 'junit:junit:4.10'
    testCompile 'org.robolectric:robolectric:2.3'
    testCompile 'com.squareup:fest-android:1.0.8'
    testCompile 'org.mockito:mockito-core:1.10.5'
}

task androidJavadocs(type: Javadoc) {
    source = android.sourceSets.main.java.srcDirs
}

task androidJavadocsJar(type: Jar) {
    classifier = 'javadoc'
    from androidJavadocs.destinationDir
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.srcDirs
}

artifacts {
    archives androidSourcesJar
    archives androidJavadocsJar
}

if (project.hasProperty('sonatype.username') && project.hasProperty('sonatype.password')) {
    signing {
        sign configurations.archives
    }

    uploadArchives {
        repositories {
            mavenDeployer {
                beforeDeployment { deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(
                            userName: project.getProperty('sonatype.username'),
                            password: project.getProperty('sonatype.password'))
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots") {
                    authentication(
                            userName: project.getProperty('sonatype.username'),
                            password: project.getProperty('sonatype.password'))
                }
            }
        }

        pom.project {
            name 'Traffic Cop'
            packaging 'aar'
            description 'Monitor your Android app\'s data usage so you can take action if it\'s over a threshold.'
            url 'https://github.com/willowtreeapps/trafficcop'
            scm {
                url 'git@github.com:willowtreeapps/trafficcop.git'
                connection 'scm:git:git@github.com:willowtreeapps/trafficcop.git'
                developerConnection 'scm:git:git@github.com:willowtreeapps/trafficcop.git'
            }
            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            developers {
                developer {
                    id 'evant'
                    name 'Evan Tatarka'
                }
            }
        }
    }
}

